plugins {
    alias(libs.plugins.licenser) apply false
    alias(libs.plugins.loom) apply false
    id 'base'
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

defaultTasks 'build'

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'me.lucko.luckperms'
    version = '5.5-SNAPSHOT'

    java {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Test).configureEach {
        testLogging {
            events = [TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED]
            exceptionFormat = TestExceptionFormat.FULL
            showExceptions = true
            showCauses = true
            showStackTraces = true
        }
    }

    jar {
        from '../LICENSE.txt'
    }

    project.ext.majorVersion = '5'
    project.ext.minorVersion = '5'
    project.ext.patchVersion = '0'
    project.ext.apiVersion = project.ext.majorVersion + '.' + project.ext.minorVersion
    project.ext.fullVersion = project.ext.apiVersion + '.' + project.ext.patchVersion

    repositories {
        // Fix issue with lwjgl-freetype not being found on macOS / ForgeGradle issue
        //
        // Could not resolve all files for configuration ':_compileJava_1'.
        // Could not find lwjgl-freetype-3.3.3-natives-macos-patch.jar (org.lwjgl:lwjgl-freetype:3.3.3).
        maven {
            url "https://libraries.minecraft.net"
            content {
                includeModule("org.lwjgl", "lwjgl-freetype")
            }
        }
        mavenCentral()
        maven { url 'https://repo.lucko.me/' }
        maven { url 'https://libraries.minecraft.net/' }
    }
}

tasks.register('copyBukkitJars', Copy) {
    from 'bukkit/loader/build/libs/'
    into 'target/'
    include '*.jar'
}

tasks.register('cleanTarget', Delete) {
    delete 'target/'
}

clean.dependsOn('cleanTarget')

subprojects {
    tasks.matching { it.name == 'build' }.configureEach {
        finalizedBy(':copyBukkitJars')
    }
}
